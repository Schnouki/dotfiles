# -*- mode: sh; -*-

##### Environment variables
. ~/.config/zsh/env

##### History
export HISTFILE=~/.histfile
export HISTSIZE=1000
export SAVEHIST=1000
export DIRSTACKSIZE=16
setopt hist_ignore_space
setopt appendhistory

##### Colors
autoload -U colors
colors
# Nice colors in ls. Changing TERM is needed because dircolors doesn't know
# about rxvt-256color...
eval `TERM=xterm-256color dircolors -b ~/.config/zsh/colors.db`

# Add some more colors!
function _add_lscolors() {
    local color ext mimetype
    mimetype=shift; color=shift
    for ext in $(grep "$mimetype" /etc/mime.types | cut -f 2-); do
        LS_COLORS="$LS_COLORS:*.$ext=$color"
    done
    export LS_COLORS
}
_add_lscolors "audio/" "00;36"
_add_lscolors "image/" "01;35"
_add_lscolors "video/" "01;35"

##### Prompt
autoload -U promptinit
promptinit
setopt promptsubst
if [[ $TERM == "dumb" ]]; then    # in emacs
    export PS1='%(?..[%?])%!:%~%# '
    # for tramp to not hang, need the following. cf:
    # http://www.emacswiki.org/emacs/TrampMode
    unsetopt zle
    unsetopt prompt_cr
    unsetopt prompt_subst
    unfunction precmd  &>/dev/null
    unfunction preexec &>/dev/null
else
    # VCS info in the right prompt, based on http://kriener.org/articles/2009/06/04/zsh-prompt-magic
    autoload -Uz vcs_info

    FMT_BRANCH="%F{magenta}%s:%F{blue}%b%B%F{magenta}%u%c%%b%F{default}"
    FMT_ACTION="(%F{blue}%a%F{default})"

    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' unstagedstr '*'
    zstyle ':vcs_info:*' stagedstr   '^'
    zstyle ':vcs_info:*' actionformats "${FMT_BRANCH}${FMT_ACTION} / "
    zstyle ':vcs_info:*' formats       "${FMT_BRANCH} / "
    zstyle ':vcs_info:*' nvcsformats   ""

    # Detect the UID of user "schnouki" (aka me)
    local my_uid
    my_uid=$(id -u schnouki 2>/dev/null)
    [[ -z "$my_uid" ]] && my_uid=1000

    PS1="%B" # Bold font
    PS1+="%(!..%(${my_uid}#..%F{green}%n))" # Username in green if it's not schnouki or root

    # Display hostname in PS1 if we're running on a remote session (SSH, tmux, screenâ€¦)
    if [[ -z "$XDG_SEAT" ]]; then
        PS1+="%(!..%(${my_uid}#..%F{yellow}@))%F{white}%m "
    fi

    PS1+='%B%F{white}${ps1_path} ' # Path
    PS1+="%(!.%F{red}.%F{yellow})%# " # %/# sign in yellow or red (root)
    PS1+="%F{default}%b" # Restore normal font
    export PS1
    unset my_uid

    RPS1="%(?..%B%F{red}%?%b%F{default} / )" # Status code if red if not 0
    RPS1+='${vcs_info_msg_0_}' # VCS info
    RPS1+="%B%F{cyan}%*" # Time
    RPS1+="%F{default}%b" # Restore normal font
    export RPS1
    setopt transient_rprompt

    function precmd {
        # Terminal title
        case $TERM in
            xterm*|rxvt*|Eterm|screen) print -Pn "\e]0;%n@%m: %~\a" ;;
        esac

        # Update VCS prompt (without telling stuff like "fatal: This operation
        # must be run in a work tree" if we are in a .git dir)
        vcs_info 2>/dev/null

        # Path displayed in PS1 - collapse $HOME but not other variables
        ps1_path="${PWD/#$HOME/~}"
        # Collapse each dir(but the last) to its first letter
        # From  http://git.io/ffi9_A, no idea how it works :)
        if [[ ! "${ps1_path}" == "~" ]]; then
            ps1_path="${${${(@j:/:M)${(@s:/:)ps1_path}##.#?}:h}%/}/${ps1_path:t}"
        fi
        export ps1_path
    }
fi

##### Completion
# Load custom scripts
fpath=($HOME/.config/zsh/completion $fpath)
autoload -U $HOME/.config/zsh/completion/_*(.:t)
for f in $HOME/.config/zsh/completion/*.zsh(.); do
    source $f
done

# Now init the completion system
autoload -Uz compinit
compinit -u
zstyle :compinstall filename "$HOME/.zshrc"
zstyle ':completion:*' use-cache on

unsetopt list_ambiguous
setopt auto_remove_slash
setopt glob_dots
setopt chase_links
setopt hist_verify
setopt hist_ignore_all_dups
setopt auto_cd
setopt auto_pushd
setopt correctall
setopt completealiases

# Completion: complex but rich :) Inspired by a lot of sources. Here are e few
# important links:
# - http://zsh.sourceforge.net/Doc/Release/Completion-System.html
# - https://github.com/nicoulaj/dotfiles/blob/master/.zshrc#L317

# Basic resentation styles
zstyle ':completion:*:(descriptions|messages)' format '%K{53}%F{255}%B[ %d ]%b%f%k'
zstyle ':completion:*:warnings'     format '%BNo matches found%b'

# Default colors for listings.
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# Separate directories from files.
zstyle ':completion:*' list-dirs-first true

# Turn on menu selection only when selections do not fit on screen.
zstyle ':completion:*' menu select=long-list

# Separate matches into groups.
zstyle ':completion:*:matches' group yes
zstyle ':completion:*' group-name ''

# Describe options
zstyle ':completion:*:options' description yes

# Always use the most verbose completion.
zstyle ':completion:*' verbose true

# Treat sequences of slashes as single slash.
zstyle ':completion:*' squeeze-slashes true

# Avoid repeating file names when completing these commands
zstyle ':completion:*:(rm|mv|cp):*' ignore-line yes

# Nicer listings for kill
zstyle ':completion:*:*:kill:*' command 'ps -a -w -w -u $USER -o pid,cmd --sort=-pid'
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=39=32"

# Ignore hidden files by default
zstyle ':completion:*:(all-|other-|)files'  ignored-patterns '*/.*'
zstyle ':completion:*:(local-|)directories' ignored-patterns '*/.*'
zstyle ':completion:*:cd:*'                 ignored-patterns '*/.*'

# Don't complete completion functions/widgets.
zstyle ':completion:*:functions' ignored-patterns '_*'

##### Various options
setopt extendedglob

# Don't send "HUP" to running jobs when logging out
unsetopt hup

# zmv is awesome
autoload -Uz zmv

# virtualenvwrapper is nice
[[ -e /usr/bin/virtualenvwrapper.sh ]] && source /usr/bin/virtualenvwrapper.sh

# Reset WORDCHARS so it's easier to navigate between words.
# Default value: export WORDCHARS='*?_-.[]~=/&;!#$%^(){}<>'
export WORDCHARS=''

##### Tell other scripts this one has been loaded
export PROFILE_LOADED=1
