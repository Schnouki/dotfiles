#!/bin/zsh

# Setup each part of the repo after an update

# Helper functions
function setup_file_symlink() {
    local target link
    target="$HOME/.config/$1"
    link="$HOME/$2"

    if [ ! -e "$link" ]; then
        ln -s "$target" "$link"
    elif [ -L "$link" ]; then
        # Correct target?
        if [ "$(readlink "$link")" != "$target" ]; then
            echo "~/$link points to a wrong target :("
            exit 1
        fi
    else
        echo "There is something wrong with ~/$link :/"
        exit 1
    fi
}

function setup_dir_symlink() {
    local target link
    target="$HOME/.config/$1"
    link="$HOME/$2"

    if [ ! -e "$link" ]; then
        ln -s "$target" "$link"
    elif [ -L "$link" ]; then
        # Correct target?
        if [ "$(readlink "$link")" != "$target" ]; then
            echo "$link points to a wrong target :("
            exit 1
        fi
    elif [ -d "$link" ]; then
        if [ "$(/bin/ls -A "$link")" ]; then
            # Not empty: fail
            echo "~/$link exists and is not empty :("
            exit 1
        else
            rmdir "$link" && ln -s "$target" "$link"
        fi
    else
        echo "There is something wrong with ~/$link :/"
        exit 1
    fi
}

function setup_decrypt() {
    local src dst sf
    src="$HOME/.config/$1"
    dst="$HOME/$2"
    exe="$3"
    if [[ ! -x /usr/bin/gpg2 ]]; then
	echo "! gpg2 not found: ignoring $1"
	return
    fi

    # Status file name
    sf="$HOME/.config/.last_update_$(echo "$src" | md5sum | cut -d' ' -f1)"

    if [ ! -e "$dst" ] || [ ! -e "$sf" ] || [ "$src" -nt "$sf" ]; then
        echo "Decrypting $dst"
        gpg2 --output "$dst" --decrypt "$src" || exit 1
        if [ "$exe" ]; then 
            chmod 0700 "$dst"
        else
            chmod 0600 "$dst"
        fi
        touch "$sf"
    fi
}

function setup_decrypt_tarball() {
    local src sf cs
    src="$HOME/.config/$1"
    if [[ ! -x /usr/bin/gpg2 ]]; then
	echo "! gpg2 not found: ignoring $1"
	return
    fi

    # Status file name
    sf="$HOME/.config/.last_update_$(echo "$src" | md5sum | cut -d' ' -f1)"

    if [ ! -e "$sf" ] || [ "$src" -nt "$sf" ]; then
        echo "Decrypting ${src:t}"
        # zsh MULTIOS option FTW!
        { gpg2 --decrypt "$src" } >  >(tar x -C "$HOME") > >(tar xO | md5sum | cut -d' ' -f1 > "$sf")
        touch "$sf"
    fi
}

for S in setup.*; do 
    echo "Running $S..."
    source ./$S || exit 1
done
