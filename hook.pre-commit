#!/bin/zsh

# Check if it is ok to commit now
hook_exit_code=0

# Helper functions
function hook_update_encrypted_file() {
    local src dst sf
    src="$HOME/$1"
    dst="$HOME/.config/$2"
    sf="$HOME/.config/.last_update_$(echo "$dst" | md5sum | cut -d' ' -f1)"

    if [ ! -e "$dst" ] || [ "$src" -nt "$sf" ]; then
        echo "New version of $src found, encrypting it..."
        rm "$dst"
        gpg2 --quiet --batch --output "$dst" --encrypt "$src"
        touch "$sf"
        hook_exit_code=1
    fi
}

hook_update_encrypted_tarball() {
    local srcname src dst sf cs gcs
    srcname=$1
    dst="$HOME/.config/$2"
    src=(${@:3})
    sf="$HOME/.config/.last_update_$(echo "$dst" | md5sum | cut -d' ' -f1)"
    cs=$(tar c -C "$HOME" ${src/$HOME\//} | tar xO | md5sum | cut -d' ' -f1)
    [[ ! -e "$sf" ]] && touch "$sf"
    gcs=$(cat "$sf")

    if [ ! -e "$dst" ] || [ "$cs" != "$gcs" ]; then
        echo "New version of $srcname found, encrypting it..."
        tar c -C "$HOME" ${src/$HOME\//} | gpg2 --quiet --batch --encrypt > "$dst"
        echo "$cs" > "$sf"
        hook_exit_code=1    
    fi
}

# Check for sensitive configuration files: SSH, GnuPG, etc.
hook_update_encrypted_file ".ssh/config" "ssh.gpg"
hook_update_encrypted_file ".gnupg/gpg.conf" "gpg.conf.gpg"
hook_update_encrypted_file ".config/emacs/init-99-private.el" "emacs/init-99-private.el.gpg"
hook_update_encrypted_file ".config/notmuch/autotag.py" "notmuch/autotag.py.gpg"

hook_update_encrypted_tarball "Weechat" "weechat.tar.gpg" ~/.weechat/{*.conf,{perl,python}/**/**(-.)}

exit $hook_exit_code
