default: obsidian steam steam-native vivaldi-stable

prepare FILE:
    cp "/usr/share/applications/{{FILE}}.desktop" "{{FILE}}.desktop"

add_args FILE +ARGS:
    sed -i 's/^\(Exec=.*\)/\1 {{ARGS}}/g' "{{FILE}}.desktop"

add_runner FILE +RUNNER:
    sed -i '/{{RUNNER}}/!s/^Exec=\(.*\)/Exec={{RUNNER}} \1/g' "{{FILE}}.desktop"

electron_wayland FILE:
    just add_args "{{FILE}}" --enable-features=UseOzonePlatform --ozone-platform=wayland

electron_scaling FILE:
    just add_args "{{FILE}}" --force-device-scale-factor=1.25

obsidian: (prepare "obsidian")
    just electron_scaling obsidian

steam: (prepare "steam")
    just add_runner steam prime-run

steam-native: (prepare "steam-native")
    just add_runner steam-native prime-run

vivaldi-stable: (prepare "vivaldi-stable")
    sed -i "s!/usr/bin/vivaldi-stable!${HOME}/bin/vivaldi-stable!g" vivaldi-stable.desktop
    just electron_wayland vivaldi-stable

clean:
    rm -f obsidisan.desktop steam.desktop steam-native.desktop vivaldi-stable.desktop
